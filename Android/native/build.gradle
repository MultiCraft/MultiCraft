apply plugin: 'com.android.library'
apply plugin: 'de.undercouch.download'

android {
	compileSdk 35
	ndkVersion '27.2.12479018'
	defaultConfig {
		minSdkVersion 23
		targetSdkVersion 35
		externalNativeBuild {
			ndkBuild {
				arguments '-j' + Runtime.getRuntime().availableProcessors(),
						"--output-sync=none",
						"versionMajor=${versionMajor}",
						"versionMinor=${versionMinor}",
						"versionPatch=${versionPatch}",
						"versionExtra=${versionExtra}",
						"developmentBuild=${developmentBuild}"
			}
		}
	}

	externalNativeBuild {
		ndkBuild {
			path file('jni/Android.mk')
		}
	}

	// supported architectures
	splits {
		abi {
			enable true
			reset()
			//noinspection ChromeOsAbiSupport
			include 'armeabi-v7a', 'arm64-v8a', 'x86_64'
		}
	}

	buildTypes {
		release {
			externalNativeBuild {
				ndkBuild {
					arguments 'NDEBUG=1'
				}
			}
		}
	}
	namespace = "com.multicraft"
}

// get precompiled deps
def buildDirectory = layout.buildDirectory.get().asFile

tasks.register('downloadDeps', Download) {
	src "https://github.com/MultiCraft/deps_android/releases/latest/download/deps_android.zip"
	dest new File(buildDirectory, 'deps.zip')
	overwrite false
}

tasks.register('getDeps', Copy) {
	dependsOn downloadDeps
	def deps = file('deps')
	def f = file("$buildDirectory/deps_android")

	if (!f.exists()) {
		from zipTree(downloadDeps.dest)
		into deps
	}
}

preBuild.dependsOn getDeps

android.defaultConfig.externalNativeBuild.ndkBuild {
	arguments 'prebuilt=$(if $(strip $(wildcard $(prebuilt_path))),$(prebuilt_path),.)'
}
